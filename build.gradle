apply plugin: 'java'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'org.standardout.versioneye'

version = '2.4.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileJava.options.encoding = 'UTF-8'

ext {
    title = 'log4j-systemd-journal-appender'
    description = 'Log4j appender for systemd journal.'
    url = 'https://www.github.com/bwaldvogel/log4j-systemd-journal-appender'
}

jar {
    manifest {
        attributes 'Implementation-Title': title, 'Implementation-Version': version
    }
}

// https://github.com/bmuschko/gradle-nexus-plugin
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.standardout:gradle-versioneye-plugin:1.5.0'
    }
}

ext {
    log4jVersion = '2.11.1'
}

modifyPom {
    project {
        groupId        'de.bwaldvogel'
        artifactId     'log4j-systemd-journal-appender'
        name           title
        description    description
        url            url
        inceptionYear '2014'

        scm {
            url 'git@github.com:bwaldvogel/log4j-systemd-journal-appender.git'
            connection 'scm:git:git@github.com:bwaldvogel/log4j-systemd-journal-appender.git'
            developerConnection 'scm:git:git@github.com:bwaldvogel/log4j-systemd-journal-appender.git'
        }

        licenses {
            license {
                name 'The BSD License'
                url 'http://www.opensource.org/licenses/bsd-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bwaldvogel'
                name 'Benedikt Waldvogel'
                email 'mail@bwaldvogel.de'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = true
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '4.10.2'
    distributionType = 'ALL'
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'net.java.dev.jna', name: 'jna', version: '4.5.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.22.0'
}

// vim: set ts=4 sw=4 et:
